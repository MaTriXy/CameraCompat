apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
//apply from: "https://raw.githubusercontent.com/Piasy/BintrayUploadScript/master/bintray.gradle"

apt {
    arguments {
        fragmentArgsLib true
    }
}

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.releaseVersionCode
        versionName rootProject.ext.releaseVersionName

        ndk {
            moduleName "rgb-yuv-converter-library"
            stl "gnustl_shared"
            abiFilters "all"
            ldLibs "log"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jni.srcDirs = ['src/main/jni']
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:${rootProject.ext.androidSupportSdkVersion}"
    compile 'com.getkeepsafe.relinker:relinker:1.2.1'
    compile "jp.co.cyberagent.android.gpuimage:gpuimage-library:${rootProject.ext.gpuImageVersion}"

    compile "com.hannesdorfmann.fragmentargs:annotation:${rootProject.ext.fragmentArgsVersion}"
    apt "com.hannesdorfmann.fragmentargs:processor:${rootProject.ext.fragmentArgsVersion}"

    compile 'org.greenrobot:eventbus:3.0.0'
}

def libraryGroupId = 'com.github.piasy'
def libraryArtifactId = 'CameraCompat'
def libraryVersion = '1.0.0'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
        }
    }
}

artifactory {
    contextUrl = 'http://47.88.103.205:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}